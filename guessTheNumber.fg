#====================================================
#
# gameCourse
#
#====================================================
#====================================================

{deffunc gameCourse (p_minNumber p_maxNumber)
	.var numberToGuess <-- :randomIntBetween $p_minNumber $p_maxNumber
	.var tryNumber <-- 'not_defined'
	.var numberOfSteps <-- 0

	.print :+ 'Answer: ' $numberToGuess # to cheat

	:displayNewMessageIn (+ 'Guess my number (between ' $p_minNumber ' and ' $p_maxNumber ')!') '#gameBoard'
	{while [ $tryNumber /= $numberToGuess ]
		# Try
		#=====
		tryNumber <-- .awaitNewHumanNumberIn '#gameBoard'
		numberOfSteps <-- [+ 1]

		# Answer
		#========
		{ if [$tryNumber < $numberToGuess] :displayNewMessageIn 'Too low! Try again!' '#gameBoard' }
		{ if [$tryNumber > $numberToGuess] :displayNewMessageIn 'Too high! Try again!' '#gameBoard' }
		{ if [$tryNumber = $numberToGuess] :displayNewMessageIn ['Well Done! Number of steps: ' + $numberOfSteps] '#gameBoard' }
	}
	~{
		displayNewMessageIn (+ 'The number to guess was ' $numberToGuess '.') '#gameBoard'
	}
	# Return
	#=======
	$numberOfSteps
}

#====================================================
#
# main program
#
#====================================================
#====================================================

.var main
.var numGameCourse <-- 0
.var score <-- 0
.var level <-- 1
.var limitNumber <-- 2
.var theGameCourse

# main
#======================

{par @main
	
	# Game
	#--------
	{while true {seq @theGameCourse
		.var gameCourseNumberOfSteps

		# Game course
		numGameCourse <-- [+ 1]
		.waitSeconds 1
		gameCourseNumberOfSteps <-- :gameCourse 1 [$limitNumber - 1]

		# update infos
		.var diffScore <-- [[3 * $level] - $gameCourseNumberOfSteps]
		score <-- [+ $diffScore]
		{ if [$gameCourseNumberOfSteps <= $level]
			level <-- [+ 1]
			limitNumber <-- [* 2]
		}
		.waitSeconds 3
	}}

	# Interrupt
	#----------
	{while true
		.awaitClickBeep '#giveUp'
		{if [$level > 1]
			level <-- [- 1]
			limitNumber <-- [/ 2]
		}
		.break theGameCourse
	}

	# quit
	#----------
	{seq
		.awaitClickBeep '#quit'
		.break main
	}

	# Display of all pieces of information
	#-------------------------------------
	{while true
		:await numGameCourse beep
		{if [$numGameCourse > 0]
			:displayNewMessageIn (+ 'Game Course #' $numGameCourse) '#gameBoard/title'
		}
	}
	{while true
		:await score beep
		:displayMessageIn $score '#score'
		
		:addCssClassTo 'highlight' '#scoreHead'
		.waitSeconds [1 / 3]
		:DelCssClassFrom 'highlight' '#scoreHead'
		.waitSeconds [1 / 3]
		:addCssClassTo 'highlight' '#scoreHead'
		.waitSeconds [1 / 3]
		:DelCssClassFrom 'highlight' '#scoreHead'
	}
	{while true
		:await level beep
		:displayMessageIn $level '#level'
		
		:addCssClassTo 'highlight' '#levelHead'
		.waitSeconds [1 / 3]
		:DelCssClassFrom 'highlight' '#levelHead'
		.waitSeconds [1 / 3]
		:addCssClassTo 'highlight' '#levelHead'
		.waitSeconds [1 / 3]
		:DelCssClassFrom 'highlight' '#levelHead'
	}
}

# Good bye
#======================
:displayNewMessageIn (+ 'Good job! You get '  $score  ' in ' $numGameCourse ' courses.') '#gameBoard'
:displayNewMessageIn  'Good bye! See you soon! ' '#gameBoard'
